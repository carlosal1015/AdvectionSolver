# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=mesh_hydro_utils
pkgname=python-${_base//_/-}-git
pkgdesc="A collection of convenience utilities for the mesh-hydro and hydro-playground codes"
pkgver=r19.0d7b2ab
pkgrel=1
arch=(any)
url="https://github.com/mladenivkovic/${_base}"
license=(GPL-3.0-or-later)
depends=(python-matplotlib)
makedepends=(python-build python-installer python-setuptools python-wheel git)
checkdepends=(python-h5py texlive-latexextra texlive-fontsrecommended)
optdepends=('python-h5py: for create HDF5 file')
source=(git+${url}.git#branch=main
  license.patch::${url}/pull/1.patch
  numpy2.patch::${url}/pull/2.patch)
sha512sums=('SKIP'
  '4ae5eb3772a6cc9fa41fd16d82cf9f1758f6433a5d946fb46105ccac140e77a825521052834005c8675525d11df2f78e3db7ade8a5c0d8a45cd24f986dc25887'
  '862fa8955ac54bcc890ac41db1c279cb796b901494e5867402fcf26a810e17ca4a4e188326535303e9d598acc8a50c0c51f42cf0ac0fb1767024dacee4193b7f')
provides=(python-${_base//_/-})
conflicts=(python-${_base//_/-})

prepare() {
  cd ${_base}
  patch -p1 -i ../license.patch
  patch -p1 -i ../numpy2.patch
}

pkgver() {
  cd ${_base}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd ${_base}
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_base}/testing
  PYTHONPATH="${srcdir}/${_base}:${PYTHONPATH}" ./run.sh
}

package() {
  cd ${_base}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl
  # install scripts
  install -d ${pkgdir}/usr/share/${pkgname}
  mv scripts ${pkgdir}/usr/share/${pkgname}
  mv testing ${pkgdir}/usr/share/${pkgname}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
